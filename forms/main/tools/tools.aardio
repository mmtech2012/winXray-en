import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=938;bottom=638;bgcolor=16777215)
winform.add(
btnAardio={cls="plus";text="aardio desktop software rapid development (development environment only 6.5MB)";left=26;top=533;right=681;bottom=575;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF17A';notify=1;textPadding={left=50};z=3};
btnCreateLnk={cls="plus";text="Create desktop icons";left=26;top=391;right=234;bottom=433;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-23;name='FontAwesome');padding={left=18}};iconText='\uF108';notify=1;textPadding={left=50};z=10};
btnDeleteIconCache={cls="plus";text="Fix the desktop icon blank";left=26;top=439;right=269;bottom=481;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-23;name='FontAwesome');padding={left=18}};iconText='\uF0AD';notify=1;textPadding={left=50};z=16};
btnDnsFlush={cls="plus";text="Clear DNS cache";left=26;top=155;right=412;bottom=197;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF021';notify=1;textPadding={left=50};z=5};
btnGitHub={cls="plus";text="Github internet speed optimization";left=26;top=250;right=338;bottom=292;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF09B';notify=1;textPadding={left=50};z=2};
btnQrCode={cls="plus";text="QR code generation tool";left=26;top=297;right=262;bottom=339;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF029';notify=1;textPadding={left=50};z=14};
btnSshInstall={cls="plus";text="Install V2Ray server with one click";left=393;top=14;right=690;bottom=56;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=11}};iconText='\uF17C';notify=1;textPadding={left=40};z=9};
btnSshInstallKey={cls="plus";text="Install SSH keys";left=694;top=15;right=863;bottom=57;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-24;name='FontAwesome');padding={left=11}};iconText='\uF084';notify=1;textPadding={left=40};z=11};
btnTestSpeed={cls="plus";text="Current proxy server speed test";left=26;top=61;right=312;bottom=103;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-24;name='FontAwesome');padding={left=18}};iconText='\uF0ED';notify=1;textPadding={left=50};z=12};
btnUpdateSystemTime={cls="plus";text="Synchronize system time (correct V2Ray connection error)";left=26;top=108;right=510;bottom=150;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF017';notify=1;textPadding={left=50};z=4};
btnUuid={cls="plus";text="UUID generation tool";left=26;top=344;right=262;bottom=386;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF13E';notify=1;textPadding={left=50};z=15};
btnWubiLex={cls="plus";text="WIN10 Wubi Assistant (only 830KB, comes with Wubi 86, 98, 091, New Century, Zhengma...)";left=26;top=486;right=745;bottom=528;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-24;name='FontAwesome');padding={left=18}};iconText='\uF11C';notify=1;textPadding={left=50};z=6};
btnYoutube={cls="plus";text="Youtube video download tool";left=26;top=203;right=292;bottom=245;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF167';notify=1;textPadding={left=50};z=1};
editSshHost={cls="plus";left=158;top=23;right=386;bottom=47;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};dl=1;dt=1;editable="edit";font=LOGFONT(h=-16);textPadding={bottom=1};z=7};
lbCurrentProxyLocation={cls="static";left=314;top=74;right=798;bottom=101;color=32768;dl=1;dt=1;notify=1;transparent=1;z=13};
static={cls="static";text="Account@Server:";left=30;top=27;right=145;bottom=54;align="right";color=4934475;dl=1;dt=1;font=LOGFONT(h=-13);transparent=1;z=8}
)
/*}}*/

import style;
winform.btnYoutube.skin(style.plainButton);

var frmYoutube;
winform.btnYoutube.oncommand = function(id,event){
	if(frmYoutube&&win.isWindow(frmYoutube.hwnd)){
		if(win.isIconic(frmYoutube.hwnd)){
			frmYoutube.show(9/*_SW_RESTORE*/);	
		}
		
		frmYoutube.show();
		return;
	}
	
	frmYoutube = winform.loadForm("\forms\main\tools\youtube.aardio");
	frmYoutube.show();
}
	
import process;
winform.btnGitHub.skin(style.plainButton)
winform.btnGitHub.oncommand = function(id,event){
	if(_STUDIO_INVOKED){
		winform.msgboxErr("Please run this function after release");
		return;
	}
	
	process.execute(io._exepath,"/github","runas");
}

winform.btnAardio.skin(style.plainButton)
winform.btnAardio.oncommand = function(id,event){
	process.openUrl("http://www.aardio.com")
}

winform.btnUpdateSystemTime.skin(style.plainButton)
winform.btnUpdateSystemTime.oncommand = function(id,event){
	if(_STUDIO_INVOKED){
		winform.msgboxErr("Please run this function after release");
		return;
	}
	
	winform.btnUpdateSystemTime.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	if( process.executeWait(io._exepath,"/updateTime","runas") ){
		winform.btnUpdateSystemTime.disabledText = null;
		winform.btnUpdateSystemTime.text = 'Time synced'
		winform.setTimeout( 
			function(){
				winform.btnUpdateSystemTime.text = 'Synchronize system time (correct v2ray connection error)'
			},2000) 
	}
	else {
		winform.btnUpdateSystemTime.disabledText = null;
	} 
}

winform.btnDnsFlush.skin(style.plainButton)
winform.btnDnsFlush.oncommand = function(id,event){
	winform.btnDnsFlush.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}

	import process.popen;
	var prcs = process.popen(,"ipconfig /flushdns");
	var str = prcs.read(-1)
	
	var dnsapi = ..raw.loadDll("dnsapi.dll");
	dnsapi.DnsFlushResolverCacheB();
		
	winform.msgInfo(str);
	winform.btnDnsFlush.disabledText = null;
}

import process;
winform.btnWubiLex.skin(style.plainButton)
winform.btnWubiLex.oncommand = function(id,event){
	process.openUrl("http://wubi.aardio.com/");
}

winform.editSshHost.setCueBannerText("root@0.0.0.0:22")
winform.btnSshInstall.skin(style.plainButton)
winform.btnSshInstall.oncommand = function(id,event){
	
	if(_STUDIO_INVOKED){
		winform.msgErr("Please run this function after release .EXE");
		return;
	}
	
	config.ui.sshServer = winform.editSshHost.text;
	config.ui.save();
	
	winform.btnSshInstall.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	
	var server = winform.editSshHost.text;
	if( ! ..string.indexOf(server,"@") ){
		server = "root@" + server;
	}
	process.execute(io._exepath,"/sshInstall " + server);
	win.delay(1000);
	winform.btnSshInstall.disabledText = null;
}

import config;
if(config.ui.sshServer){
	winform.editSshHost.text = config.ui.sshServer;
}

winform.btnSshInstallKey.skin(style.plainButton)
winform.btnSshInstallKey.oncommand = function(id,event){
	if(_STUDIO_INVOKED){
		winform.msgErr("Please run this function after release .EXE");
		return;
	}
	
	config.ui.sshServer = winform.editSshHost.text;
	config.ui.save();
	
	winform.btnSshInstall.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	
	var server = winform.editSshHost.text;
	if( ! ..string.indexOf(server,"@") ){
		server = "root@" + server;
	}
	process.execute(io._exepath,"/sshInstallKey " + server);
	win.delay(1000);
	winform.btnSshInstall.disabledText = null;
}

winform.btnCreateLnk.skin(style.plainButton)
winform.btnCreateLnk.oncommand = function(id,event){
	if(_STUDIO_INVOKED){
		return winform.msgboxErr("Please use this feature after release .EXE")
	}
	
	import fsys.lnk;  
	var lnk = fsys.lnk(); 
	lnk.description = "winXray(V2Ray/V2Ray、Shadowsocks、Trojan Universal client)"  
	
	lnk.path = io._exepath //设置目标路径
	lnk.setIcon(io._exepath,0); //设置图标
	
	lnk.save(
		io.getSpecial(0/*_CSIDL_DESKTOP*/,"winXray.lnk" )
	) 
	
	::Shell32.SHChangeNotify(0x8000000/*_SHCNE_ASSOCCHANGED*/,0,0,0);
	
	import com;
	com.CreateObject("Shell.Application").MinimizeAll();
}

winform.btnTestSpeed.skin(style.plainButton)
winform.btnTestSpeed.oncommand = function(id,event){
	import chrome.path;
	var chromePath = chrome.path();
	if(!chromePath){
		winform.msgFrown('Sorry,\r\nThe speed measurement tool needs to call Chrome or a compatible browser to run,\r\nBut the current system does not find any Chrome compatible browser!');
		return; 
	}
	
	import v2ray.core;
	if(!v2ray.core.socksProxyPort){
		winform.msgFrown('Sorry, no proxy server is currently connected!');
		return; 
	}
	
	winform.btnTestSpeed.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	
	var ipip = win.invoke(
		function(port){
			import web.rest.client;
			var restClient = web.rest.client(,"SOCKS=127.0.0.1:"+port);
			restClient._http.setTimeouts(1000,2000,2000);
			restClient.get("https://www.google.com/generate_204");
			var lastStatusCode = restClient.lastStatusCode; 
				
			if( lastStatusCode == 204 ){
					
				var ipip,err  = restClient.get("http://myip.ipip.net");
				if(ipip){
					ipip = ..string.match(ipip,"\d+\.\d+\.\d+\.\d+\A+");
					if(ipip) {
						restClient.close();
						return "Current proxy server："+ipip;
					}
				} 
				
				restClient.close();
				return null;
			}
			restClient.close();
		},v2ray.core.socksProxyPort 
	)
	
	if(!ipip){
		winform.msgFrown('Sorry, no proxy server is currently connected!');
		winform.btnTestSpeed.disabledText = null;
		return; 	
	}
	
	winform.lbCurrentProxyLocation.text = ipip;
	
	import process
	process.execute(chromePath,` --proxy-server="SOCKS5://127.0.0.1:`+v2ray.core.socksProxyPort 
		+`" --user-data-dir="` +io.appData("winXray/chrome-socks5")+ `" --app="https://speed.cloudflare.com/"`);
	winform.btnTestSpeed.disabledText = null;
}

onActiveOutbound = function(address){
	winform.btnTestSpeed.disabled = !address;
	if(type.isString(address)){
		winform.lbCurrentProxyLocation.text = "Currently active proxy server:" + address;
	}	
} 
	
subscribe("activeOutbound",function(address){ 
	onActiveOutbound(address)
} )
if(globalActiveOutbound){
	onActiveOutbound(globalActiveOutbound)	
}

winform.btnQrCode.skin(style.plainButton)
winform.btnQrCode.oncommand = function(id,event){

	var frmChild = winform.loadForm("\forms\main\tools\qr.aardio");
	frmChild.show();
	
}

winform.btnUuid.skin(style.plainButton)
winform.btnUuid.oncommand = function(id,event){
	var frmChild = winform.loadForm("\forms\main\tools\uuid.aardio");
	frmChild.show();
}

winform.btnDeleteIconCache.skin(style.plainButton)
winform.btnDeleteIconCache.oncommand = function(id,event){
	import fsys;
	import process;
	
	var explorerPath = process.kill("explorer.exe")
	if( explorerPath ) {
		fsys.delete(io.appData("iconcache.db"));  
		process.execute(explorerPath);
		
		::Shell32.SHChangeNotify(0x8000000/*_SHCNE_ASSOCCHANGED*/,0,0,0);
	} 
}

winform.lbCurrentProxyLocation.oncommand = function(id,event){
	
}

winform.enableDpiScaling();
winform.show();
win.loopMessage();