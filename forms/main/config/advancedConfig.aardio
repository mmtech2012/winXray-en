import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=892;bottom=573;bgcolor=16777215)
winform.add(
btnOpenFirewall={cls="plus";text="Set firewall";left=358;top=404;right=488;bottom=434;align="left";db=1;dl=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF09C';notify=1;textPadding={left=30};z=7};
btnUpdate={cls="plus";text="Save";left=516;top=514;right=619;bottom=550;align="left";bgcolor=11580047;db=1;dl=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=22}};iconText='\uF0C7';notify=1;textPadding={left=40};z=5};
chkEnableGitConfigGithub={cls="plus";text="Set the git client to enable github.com proxy acceleration (support https, ssh protocol)";left=147;top=435;right=798;bottom=466;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF0C8';notify=1;textPadding={left=19};z=13};
chkLan={cls="plus";text="Allow connections from LAN";left=147;top=404;right=381;bottom=435;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF0C8';notify=1;textPadding={left=19};z=1};
chkSystemStart={cls="plus";text="Launch at Windows startup";left=146;top=463;right=662;bottom=494;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF0C8';notify=1;textPadding={left=19};z=17};
editHttpPort={cls="plus";left=278;top=126;right=358;bottom=150;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};db=1;dl=1;editable="edit";font=LOGFONT(h=-16);textPadding={bottom=1};z=14};
editLanPacUrl={cls="plus";left=280;top=357;right=699;bottom=381;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};db=1;dl=1;editable="edit";font=LOGFONT(h=-16);textPadding={bottom=1};z=11};
editPacPort={cls="plus";left=278;top=281;right=358;bottom=305;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};db=1;dl=1;editable="edit";font=LOGFONT(h=-16);textPadding={bottom=1};z=2};
editPacUrl={cls="plus";left=278;top=318;right=697;bottom=342;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};db=1;dl=1;editable="edit";font=LOGFONT(h=-16);notify=1;textPadding={bottom=1};z=8};
editSocksPort={cls="plus";left=278;top=91;right=358;bottom=115;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};db=1;dl=1;editable="edit";font=LOGFONT(h=-16);textPadding={bottom=1};z=3};
hotkey={cls="hotkey";left=227;top=249;right=367;bottom=269;clip=1;db=1;dl=1;edge=1;z=26};
lbMaxTestServers={cls="static";text="100";left=751;top=54;right=1067;bottom=80;bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-13);notify=1;transparent=1;z=25};
lbTestInterval={cls="static";text="15 sec";left=682;top=22;right=941;bottom=48;bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-13);transparent=1;z=20};
lnkVersion={cls="plus";text="Checking for new version";left=15;top=533;right=376;bottom=564;align="left";color=8388608;db=1;dl=1;font=LOGFONT(h=-13);iconColor=2960685;iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF021';notify=1;paddingLeft=20;z=10};
plus={cls="plus";text="Global/PAC proxy switch hotkey";left=368;top=248;right=702;bottom=270;align="left";db=1;dl=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(name='FontAwesome');padding={left=9}};iconText='\uF11C ';notify=1;tabstop=1;textPadding={left=25};z=27};
radioHttpPac={cls="plus";text="only HTTP";left=258;top=167;right=365;bottom=198;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF111 ';notify=1;textPadding={left=19};z=21};
radioHttpProxy={cls="plus";text="HTTP( recommend )";left=378;top=205;right=562;bottom=236;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF111 ';notify=1;textPadding={left=19};z=29};
radioSocksPac={cls="plus";text="SOCKS5/SOCKS4/HTTP automatic compatibility (recommended)";left=378;top=167;right=892;bottom=198;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF111 ';notify=1;textPadding={left=19};z=22};
radioSocksProxy={cls="plus";text="SOCKS4";left=273;top=205;right=371;bottom=236;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF111 ';notify=1;textPadding={left=19};z=28};
static={cls="static";text="SOCKS Port";left=68;top=95;right=266;bottom=121;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=4};
static10={cls="static";text="PAC mode：";left=70;top=170;right=243;bottom=196;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=30};
static11={cls="static";text="Global proxy mode:";left=8;top=208;right=266;bottom=234;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=31};
static2={cls="static";text="PAC Port：";left=136;top=286;right=266;bottom=312;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=6};
static3={cls="static";text="Local PAC Address：";left=108;top=325;right=266;bottom=351;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=9};
static4={cls="static";text="LAN PAC Address: ";left=108;top=364;right=266;bottom=390;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=12};
static5={cls="static";text="HTTP  Port";left=8;top=132;right=266;bottom=158;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=15};
static6={cls="static";text="Support SOCKS4、SOCKS4A、SOCKS5";left=366;top=96;right=653;bottom=122;bgcolor=16777215;color=5921370;db=1;dl=1;font=LOGFONT(h=-13);transparent=1;z=16};
static7={cls="static";text="Interval for detecting server abnormality";left=74;top=23;right=368;bottom=49;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=19};
static8={cls="static";text="Number of servers of servers speed test simultaneously";left=20;top=56;right=426;bottom=86;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=24};
tbMaxTestServers={cls="plus";left=444;top=61;right=725;bottom=73;bgcolor=-2512093;border={radius=-1};color=23807;db=1;dl=1;foreRight=13;forecolor=-14911489;notify=1;paddingBottom=5;paddingTop=5;z=23};
tbTestInterval={cls="plus";left=386;top=28;right=667;bottom=40;bgcolor=-2512093;border={radius=-1};color=23807;db=1;dl=1;foreRight=13;forecolor=-14911489;notify=1;paddingBottom=5;paddingTop=5;z=18}
)
/*}}*/

import style;
winform.chkLan.skin(style.checkBox);  
winform.chkEnableGitConfigGithub.skin(style.checkBox);  
winform.chkSystemStart.skin(style.checkBox); 
winform.radioHttpProxy.skin(style.radio)
winform.radioSocksProxy.skin(style.radio)
winform.radioHttpPac.skin(style.radio)
winform.radioSocksPac.skin(style.radio)
winform.btnUpdate.skin(style.primaryButton);
winform.editSocksPort.skin(style.edit);
winform.editPacPort.skin(style.edit);
winform.editPacUrl.editBox.readonly = true;

import win.ui.tooltip;
var tooltipCtrl = win.ui.tooltip( winform );
tooltipCtrl.addTool(winform.editSocksPort,"Right click to copy IP:port" ); 
tooltipCtrl.addTool(winform.radioHttpProxy,"The global proxy uses the HTTP proxy port to avoid using the local DNS to resolve domain names." );
tooltipCtrl.addTool(winform.radioSocksProxy,'When the global proxy uses the SOCKS proxy port, Chrome browser, etc. will use the SOCKS4 protocol by default, and SOCKS4 will use the local DNS to resolve domain names.' );
tooltipCtrl.addTool(winform.radioHttpPac,"PAC only uses HTTP proxy port, this option is not recommended." );
tooltipCtrl.addTool(winform.radioSocksPac,'After enabling this option:\nChrome browser will give priority to using the efficient and secure SOCKS 5 protocol. \nIt can also be automatically compatible with applications that only support SOCKS4 or HTTP proxy' );

import v2ray.pacServer;
onPacUpdated = function(){
	winform.editPacPort.text = v2ray.pacServer.getPort() || config.proxy.pacPort || 1083;
	
	var pac = v2ray.pacServer.getUrl();
	winform.editPacUrl.text = pac ? string.match(pac,"[^?]+");
	pac = v2ray.pacServer.getUrl(true);
	winform.editLanPacUrl.text = pac ? string.match(pac,"[^?]+");
	
	winform.editLanPacUrl.disabled = !winform.chkLan.checked;
}

subscribe("pacServer.restarted",function(pacPort){
	onPacUpdated()
}) 

subscribe("sysProxy.modeChanged",function(mode){
	if(mode==="pac"){
		var pac = v2ray.pacServer.getUrl();
		winform.editPacUrl.text = pac ? string.match(pac,"[^?]+");
		pac = v2ray.pacServer.getUrl(true);
		winform.editLanPacUrl.text = pac ? string.match(pac,"[^?]+");	
	}
}) 

import v2ray.core;
var onInboundsUpdated = function(){
	var inbounds = ..config.core.default[["inbounds"]];
	winform.editSocksPort.text = v2ray.core.socksProxyPort || inbounds[[1]][["port"]] || 1081;
	winform.editHttpPort.text = v2ray.core.httpProxyPort || inbounds[[2]][["port"]] || 1082;
	winform.chkLan.checked = inbounds[[1]][["listen"]] !== "127.0.0.1";
	winform.btnOpenFirewall.disabled = !winform.chkLan.checked;
}

subscribe("config.core.changed",function(){
	onInboundsUpdated();
})  

subscribe("v2RayCore.restarted",function(socksProxyPort,httpProxPort){
	onInboundsUpdated();
})  

import win.reg;
var reg = win.reg("HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Run\");
winform.chkSystemStart.checked = reg.queryValue("winXray");
reg.close();

winform.chkEnableGitConfigGithub.checked = config.proxy.enableGitConfigGithub;
winform.radioSocksProxy.radioGroup = "proxy.mode";
winform.radioHttpProxy.radioGroup = "proxy.mode";
winform.radioHttpPac.radioGroup = "pac.mode";
winform.radioSocksPac.radioGroup = "pac.mode";
winform.radioHttpProxy.checked = config.proxy.useHttpGlobal;
winform.radioSocksProxy.checked = !config.proxy.useHttpGlobal;
winform.radioHttpPac.checked = !config.proxy.useSocksPac;
winform.radioSocksPac.checked = config.proxy.useSocksPac;
winform.hotkey.sethotkey(config.proxy.hotkey[1],config.proxy.hotkey[2])
onInboundsUpdated();
onPacUpdated();

import wsock.tcp.client;
winform.btnUpdate.oncommand = function(id,event){
	winform.btnUpdate.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'};
	config.proxy.useHttpGlobal = winform.radioHttpProxy.checked;
	config.proxy.useSocksPac = winform.radioSocksPac.checked;
	v2ray.pacServer.updateUseHttpProxy(!config.proxy.useSocksPac);
	
	var client = wsock.tcp.client();  
	var oldPort = v2ray.core.socksProxyPort;
	if( tonumber(winform.editSocksPort.text)
		&& tonumber(winform.editSocksPort.text) != oldPort
		&& client.connectTimeout("127.0.0.1",tonumber(winform.editSocksPort.text) ,0.5) ){
		winform.editSocksPort.editBox.showErrorTip(,"Port is already occupied");
		winform.btnUpdate.disabledText = null;
		return client.close(); 
	} 
	client.close();  
	
	var client = wsock.tcp.client();  
	var oldPacPort = v2ray.pacServer.getPort();
	if( tonumber(winform.editPacPort.text)
		&& tonumber(winform.editPacPort.text) != oldPacPort
		&& client.connectTimeout("127.0.0.1",tonumber(winform.editPacPort.text) ,0.5) ){
		winform.editPacPort.editBox.showErrorTip(,"PAC port is already occupied");
		winform.btnUpdate.disabledText = null;
		return client.close(); 
	} 
	client.close(); 
	
	
	if(winform.chkEnableGitConfigGithub.checked!=config.proxy.enableGitConfigGithub){
		config.proxy.enableGitConfigGithub = winform.chkEnableGitConfigGithub.checked;
			
		import v2ray.github;
		if(!config.proxy.enableGitConfigGithub){
			v2ray.github.setProxy(false);		
		} 
		else {
			v2ray.github.setProxy(null);	
		}
	} 
	
	var restartCore = false;
	var port = tonumber(winform.editSocksPort.text) 
	config.core.default.inbounds[1]["port"]  = port;
	if(port!=oldPort){ 
		restartCore = true;
	}	
	
	var port = tonumber(winform.editHttpPort.text) 
	config.core.default.inbounds[2]["port"]  = port;
	if(port!=oldPort){
		restartCore = true;
	}		

	var listen = winform.chkLan.checked ? "0.0.0.0" : "127.0.0.1"; 
	if(listen!=config.core.default.inbounds[1]["listen"]){
		config.core.default.inbounds[1]["listen"]= listen;
		restartCore = true;
	}	
	
	if(listen!=config.core.default.inbounds[2]["listen"]){
		config.core.default.inbounds[2]["listen"]= listen;
		restartCore = true;
	}			 
	
	config.core.save(); 
	publish("config.inbounds.changed");
	if(restartCore) {
		publish("uiCommand.restartV2RayCore");    
	}
	
	config.proxy.pacPort  = tonumber(winform.editPacPort.text)
	if(oldPacPort!=tonumber(winform.editPacPort.text)){ 
		v2ray.pacServer.restart();
	}
	
	config.proxy.save();
	sysProxy.reset(true);
	
	import win.reg;
	var reg = win.reg("HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Run\");
	if(!_STUDIO_INVOKED){
		if(winform.chkSystemStart.checked){
			reg.setSzValue("winXray",`"`+io._exepath+`" /tray`);
		}
		else {
			reg.delValue("winXray");
		}           	
	} 
	reg.close();
	
	var hotmod,hotkey = winform.hotkey.gethotkey()
	config.proxy.hotkey = {hotmod;hotkey};
	config.proxy.save();
	
	publish("uiCommand.HotkeyChanged");
	
	winform.btnUpdate.disabledText = null;
	winform.msgOk("Configuration updated",1200);
}

winform.editPacUrl.editBox.readonly = true;
winform.editLanPacUrl.editBox.readonly = true;

import win.clip;
import wsock.tcp.client;
winform.editSocksPort.editBox.enablePopMenu(function(){
	var lanIp = wsock.tcp.client.getLocalIp("www.baidu.com");
	return {
			{ "Copy port" + winform.editSocksPort.text;
				function(id){ 
					win.clip.write("" + winform.editSocksPort.text)	
				}
			};
			{ "Copy the local IP and port 127.0.0.1:" + winform.editSocksPort.text;
				function(id){ 
					win.clip.write("127.0.0.1:" + winform.editSocksPort.text)	
				}
			}; 
			{ "Copy LAN IP and port "+lanIp+":" + winform.editSocksPort.text;
				function(id){ 
					win.clip.write(lanIp+":" + winform.editSocksPort.text)
				}
			}; 
	}
} )

winform.editHttpPort.editBox.enablePopMenu(function(){
	var lanIp = wsock.tcp.client.getLocalIp("www.baidu.com");
	return {
			{ "Copy port" + winform.editHttpPort.text;
				function(id){ 
					win.clip.write("" + winform.editHttpPort.text)	
				}
			};
			{ "Copy the local IP and port 127.0.0.1:" + winform.editHttpPort.text;
				function(id){ 
					win.clip.write("127.0.0.1:" + winform.editHttpPort.text)	
				}
			}; 
			{ "Copy LAN IP and port "+lanIp+":" + winform.editHttpPort.text;
				function(id){ 
					win.clip.write(lanIp+":" + winform.editHttpPort.text)
				}
			}; 
	}
} )

winform.editPacUrl.setCueBannerText("The native PAC proxy is not currently enabled");
winform.editLanPacUrl.setCueBannerText("LAN PAC proxy is not currently enabled");

winform.chkLan.oncommand = function(id,event){
    winform.editLanPacUrl.editBox.setsel(0);
	winform.editLanPacUrl.disabled = !winform.chkLan.checked;
	winform.btnOpenFirewall.disabled = !winform.chkLan.checked;
}

winform.tbTestInterval.setTrackbarRange(1,30);
winform.tbTestInterval.skin(style.trackbar);
winform.tbTestInterval.progressPos = config.proxy.testInterval;
winform.lbTestInterval.text = config.proxy.testInterval+"second";
winform.tbTestInterval.onPosChanged = function( pos,thumbTrack ){
	winform.lbTestInterval.text = pos+"second";
	config.proxy.testInterval = pos;
	config.proxy.save();
}

winform.tbMaxTestServers.setTrackbarRange(50,1000);
winform.tbMaxTestServers.skin(style.trackbar);
winform.tbMaxTestServers.progressPos = config.proxy.maxTestServers;
winform.lbMaxTestServers.text = config.proxy.maxTestServers+"Servers";
winform.tbMaxTestServers.onPosChanged = function( pos,thumbTrack ){
	if(pos>300){
		winform.lbMaxTestServers.color = 0xFF;
		winform.lbMaxTestServers.text = pos+"Servers (the value is too large may cause lag )";
	}
	else {
		winform.lbMaxTestServers.color = 0;
		winform.lbMaxTestServers.text = pos+"Servers";
	}
	
	config.proxy.maxTestServers = pos;
	config.proxy.save();
}

if(table.getByNamespace("fsys.update.simpleMain")){
	fsys.update.simpleMain.onStatusChanged(function(version,description,status){
		/*注意此回调可捕获到调用前或调用后的更新状态变更*/
		if(status=="ready"){
			winform.lnkVersion.disabledText = null;	
			winform.lnkVersion.text = "Click here to restart the software and update to the new version：" + version
			tooltipCtrl.addTool(winform.lnkVersion,description);
		}
		elseif(status=="complete"){
			winform.lnkVersion.text = "Updated to the latest version:" + version; 
			tooltipCtrl.addTool(winform.lnkVersion,description);
			..publish("uiCommand.print",winform.lnkVersion.text );
		} 
		elseif(status=="latest"){
			winform.lnkVersion.disabledText = null;
			winform.lnkVersion.text = "Already the latest version:";
			
		} 
		elseif(status=="failed"){
			winform.lnkVersion.disabledText = null; 
			..publish("uiCommand.print",description);
			..publish("uiCommand.print","It is recommended to run the [github optimization tool] in the [tools] to fix the problem that raw.githubusercontent.com cannot be accessed."); 
		}
	})
	
	winform.lnkVersion.skin(style.link)
	tooltipCtrl.addTool(winform.lnkVersion,"Click to check for updates" );
	winform.lnkVersion.oncommand = function(id,event){ 
		
		winform.lnkVersion.text = fsys.update.simpleMain.getReadyStatusInfo() ? "Starting automatic update": "Checking for new version";
		winform.lnkVersion.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
		if(fsys.update.simpleMain.checkUpdate(true)){
			win.quitMessage();
		}
	}
}
else {
	winform.lnkVersion.close();
}


winform.btnOpenFirewall.skin(style.plainButton)
winform.btnOpenFirewall.oncommand = function(id,event){
	import process.control;
	process.control("firewall.cpl")	
}

winform.editPacUrl.skin(
	background = { 
		hover = "/res/images/btn-hover.png";
		focus = "/res/images/btn-focus.jpg";
		active = "/res/images/btn-active.png";
		disabled = "/res/images/btn-disabled.png"; 
	}
)
winform.editPacUrl.oncommand = function(id,event){
	
}

winform.lbMaxTestServers.oncommand = function(id,event){
	
}

winform.show();
win.loopMessage();
return winform;